plugins {
	id 'com.android.application'
	id 'org.jetbrains.kotlin.android'
	id 'dagger.hilt.android.plugin'
	id 'kotlin-kapt'
}

apply from: "../test_dependencies.gradle"

android {
	compileSdk application.compileSdk

	defaultConfig {
		applicationId application.id
		minSdk application.minSdk
		targetSdk application.targetSdk
		versionCode application.version
		versionName application.versionName

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		vectorDrawables {
			useSupportLibrary true
		}

		manifestPlaceholders = [auth0Domain: "@string/com_auth0_domain", auth0Scheme: "https"]
	}

	buildTypes {
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
		}
	}
	compileOptions {
		sourceCompatibility versions.java
		targetCompatibility versions.java
	}
	kotlinOptions {
		jvmTarget = versions.jvm
	}
	buildFeatures {
		compose true
	}
	composeOptions {
		kotlinCompilerExtensionVersion versions.compose
	}
	packagingOptions {
		resources {
			excludes += '/META-INF/{AL2.0,LGPL2.1}'
		}
	}
}

dependencies {
	implementation project(":ui")
	implementation project(":common")
	implementation project(":models")
	implementation project(":data")
	implementation project(":database")
	implementation project(":feature-auth")
	implementation project(":feature-account")
	implementation project(":feature-channels")
	implementation project(":feature-feed")
	implementation project(":feature-messages")
	implementation project(":feature-people")

	implementation "androidx.core:core-ktx:${versions.coreKtx}"
	implementation "androidx.activity:activity-compose:${versions.composeActivity}"
	implementation "com.google.dagger:hilt-android:${versions.hilt}"
	kapt "com.google.dagger:hilt-compiler:${versions.hilt}"

	// UI
	implementation "androidx.compose.material:material:${versions.material2}"
	implementation "androidx.core:core-splashscreen:${versions.splashscreen}"
	implementation "io.coil-kt:coil-compose:${versions.coil}"

	// Misc
	implementation "com.jakewharton.timber:timber:${versions.timber}"
	implementation "org.jetbrains.kotlinx:kotlinx-datetime:${versions.datetime}"
}
