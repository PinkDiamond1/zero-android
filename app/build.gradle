plugins {
	id 'com.android.application'
	id 'org.jetbrains.kotlin.android'
	id 'dagger.hilt.android.plugin'
	id 'kotlin-kapt'
	id 'com.google.firebase.crashlytics'
	id 'com.google.gms.google-services'
	id 'com.onesignal.androidsdk.onesignal-gradle-plugin'
}

apply from: "../test_dependencies.gradle"

android {
	compileSdk application.compileSdk

	defaultConfig {
		applicationId application.id
		minSdk application.minSdk
		targetSdk application.targetSdk
		versionCode application.version
		versionName application.versionName

		testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
		vectorDrawables {
			useSupportLibrary true
		}

		manifestPlaceholders = [auth0Domain: "@string/com_auth0_domain", auth0Scheme: "$applicationId"]
	}

	buildTypes {
		debug {
			def properties = getEnv("development")
			buildConfigField "String", "ONESIGNAL_ID", "\"${properties["one_signal_id"]}\""
		}
		release {
			minifyEnabled false
			proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

			def properties = getEnv("production")
			buildConfigField "String", "ONESIGNAL_ID", "\"${properties["one_signal_id"]}\""
		}
	}
	compileOptions {
		sourceCompatibility versions.java
		targetCompatibility versions.java
	}
	kotlinOptions {
		jvmTarget = versions.jvm
	}
	buildFeatures {
		compose true
	}
	composeOptions {
		kotlinCompilerExtensionVersion versions.composeCompiler
	}
	packagingOptions {
		resources {
			excludes += '/META-INF/{AL2.0,LGPL2.1}'
		}
	}
}

dependencies {
	implementation project(":ui")
	implementation project(":navigation")
	implementation project(":common")
	implementation project(":models")
	implementation project(":sync")
	implementation project(":network")
	implementation project(":database")
	implementation project(":datastore")
	implementation project(":data")
	implementation project(":feature-auth")
	implementation project(":feature-account")
	implementation project(":feature-channels")
	implementation project(":feature-feed")
	implementation project(":feature-messages")
	implementation project(":feature-people")

	implementation "androidx.core:core-ktx:${versions.coreKtx}"
	implementation "com.google.dagger:hilt-android:${versions.hilt}"
	implementation "androidx.hilt:hilt-work:${versions.hiltWorkManager}"
	kapt "com.google.dagger:hilt-compiler:${versions.hilt}"

	implementation platform("com.google.firebase:firebase-bom:${versions.firebase}")
	implementation "com.google.firebase:firebase-crashlytics-ktx"
	implementation "com.onesignal:OneSignal:${versions.onesignal}"

	// UI
	implementation "androidx.activity:activity-compose:${versions.composeActivity}"
	implementation "androidx.core:core-splashscreen:${versions.splashscreen}"
	implementation "io.coil-kt:coil-compose:${versions.coil}"

	// Misc
	implementation "com.jakewharton.timber:timber:${versions.timber}"
	implementation "org.jetbrains.kotlinx:kotlinx-datetime:${versions.datetime}"
}
